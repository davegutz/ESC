* ------------------------------------------
* LM2907-8 - 8 pin version, Frequency to Voltage Converter
*
*                 +IN 
*                 |  C1 
*                 |  |  C2,R1,CP+
*                 |  |  |  EM_OUT
*                 |  |  |  |  COLL_OUT 
*                 |  |  |  |  |  VCC 
*                 |  |  |  |  |  |  CP-
*                 |  |  |  |  |  |  |  GND
* **********      1  2  3  4  5  6  7  8
.SUBCKT LM2907-8 i1 i2 i3 i4 i5 i6 i7 i8
R1  i1 80 10k
Q5  i6 83 81 0 QNN
D1  81 i8 D
R81 83 i8 1.6k
R82 84 83 1.6k
Q6  i6 85 84 0 QNN
I1  i6 85 200µ
D2  85 86 D
D3  86 i8 D
R80 81 80 1m
.model Z68 d(is=.88f rs=.25 cjo=175p m=.55 nbv=1.7 bv=6.8 vj=.75 isr=1.86n
+nr=2 ibv=20.245m ibvl=1.96m nbvl=15 tbv1=-21.3u vpk=6.8)
.model QNN NPN
.model QNP PNP
.model D D
XU1 80 i8 i2 i3 i3 i7 i4 i5 i6 i8 LM29x7
.ends LM2907-8
* ------------------------------------------
* LM2907 14-pin version, Frequency to Voltage Converter
*
*                  +IN 
*                  |  -IN 
*                  |  |  C1 
*                  |  |  |  C2,R1 
*                  |  |  |  |  COMP+IN 
*                  |  |  |  |  |  COMP-IN 
*                  |  |  |  |  |  |  EM_OUT 
*                  |  |  |  |  |  |  |  COLL_OUT 
*                  |  |  |  |  |  |  |  |  VCC 
*                  |  |  |  |  |  |  |  |  |  GND
* **********       1  2  3  4  5  6  7  8  9  10
.SUBCKT LM2907-14  1 11  2  3  4 10  5  8  9  12 
R1  1 80 10k
XU1 80 11 2 3 4 10 5 8 9 12 LM29x7
.ends LM2907-14
* ------------------------------------------
* LM2917-8 - 8 pin version, Frequency to Voltage Converter
*                 +IN 
*                 |  C1 
*                 |  |  C2,R1,CP+
*                 |  |  |  EM_OUT
*                 |  |  |  |  COLL_OUT 
*                 |  |  |  |  |  VCC 
*                 |  |  |  |  |  |  CP-
*                 |  |  |  |  |  |  |  GND
* **********      1  2  3  4  5  6  7  8
.SUBCKT LM2917-8 i1 i2 i3 i4 i5 i6 i7 i8
R1  i1 80 10k
Q5  i6 83 81 0 QNN
D1  81 i8 D
R81 83 i8 1.6k
R82 84 83 1.6k
Q6  i6 85 84 0 QNN
I1  i6 85 200µ
D8  33 31 Z68
D2  85 86 D
D3  86 i8 D
R32 31 32 750
Q34 i8 32 31 0 QNP
Q33 32 33 i8 0 QNN
R33 33 i8 750
R30 i6 31 1m
R80 81 80 1m
.model Z68 d(is=.88f rs=.25 cjo=175p m=.55 nbv=1.7 bv=6.8 vj=.75 isr=1.86n
+nr=2 ibv=20.245m ibvl=1.96m nbvl=15 tbv1=-21.3u vpk=6.8)
.model QNN NPN
.model QNP PNP
.model D D
XU1 80 i8 i2 i3 i3 i7 i4 i5 i6 i8 LM29x7
.ends LM2917-8
* ------------------------------------------
* LM2917-14 - 14 pin version, Frequency to Voltage Converter
*                  +IN 
*                  |  -IN 
*                  |  |  C1 
*                  |  |  |  C2,R1 
*                  |  |  |  |  COMP+IN 
*                  |  |  |  |  |  COMP-IN 
*                  |  |  |  |  |  |  EM_OUT 
*                  |  |  |  |  |  |  |  COLL_OUT 
*                  |  |  |  |  |  |  |  |  VCC 
*                  |  |  |  |  |  |  |  |  |  GND
* **********       1  2  3  4  5  6  7  8  9  10
.SUBCKT LM2917-14  1 11  2  3  4 10  5  8  9  12
R1   1 80 10k
R30  9 31 1m
D8  33 31 Z68
R32 31 32 750
Q34 12 32 31 QNP
Q33 32 33 12 QNN
R33 33 12 750
.model Z68 d(is=.88f rs=.25 cjo=175p m=.55 nbv=1.7 bv=6.8 vj=.75 isr=1.86n
+nr=2 ibv=20.245m ibvl=1.96m nbvl=15 tbv1=-21.3u vpk=6.8)
.model QNN NPN
.model QNP PNP
XU2 80 11  2  3  4 10  5  8  9  12 LM29x7
.ends LM2917-14
*
* ------------------------------------------
* LM29x7
* Adapted from Kincaid Engineering's LM2907 model (sw 4/30/2010)
* -----------------
* Kincaid Engineering
* model of frequency to voltage converter
* if you have problems, contact Russ Kincaid
* ***** russlk.nospam(at)yahoo.com ****************
* SPICE_NET
* .TRAN 10US 60MS 40MS
* .OPTIONS LIMPTS 1E4 ITL5=1E5
* *INCLUDE DEVICE.LIB
* ALIAS  V(3)=V(3)
* ALIAS  V(1)=INPUT
* ALIAS  V(2)=V(2)
* ALIAS  V(5)=PIN5
* .PRINT TRAN  V(3)  V(1)  V(2)  V(5) 
* R2 0 12 .01
* V1 1 0 SIN 0 1 400
* CEXT1 2 0 .01UF
* C2EXT 3 0 .1UF
* R1EXT 3 0 100K
* R11 5 0 1K
* V2 9 0 7.5
* XIC 1 0 2 3 3 5 5 9 9 0 LM2907N
*
*              +IN 
*               | -IN 
*               |  | C1 
*               |  | | C2,R1 
*               |  | | | COMP+IN 
*               |  | | | |  COMP-IN 
*               |  | | | |  | EM_OUT 
*               |  | | | |  | | COLL_OUT 
*               |  | | | |  | | | VCC 
*               |  | | | |  | | | | GND
* **********    1  2 3 4 5  6 7 8 9 10
.SUBCKT LM29x7  1 11 2 3 4 10 5 8 9 12 
* -IN +IN C1 C2,R1 COMP+IN COMP-IN EM_OUT COLL_OUT VCC GND
* Note: Mr. Kincaid had the connections reversed for +IN/-IN.  (sw)  
*
*NOTE: THE REAL DEVICE OUTPUT ON PIN 3 SATURATES AT ABOUT VCC/2
*THIS MODEL OUTPUT ON PIN 3 HAS NO LIMIT ALTHO IT WILL BECOME
* NONLINEAR IF THE UPPER FREQUENCY IS EXCEEDED
*
.model QNN NPN
.model QNP PNP
.MODEL MOSFET NMOS
 E1  26 12 11  1 1E3
 E2  25 12 12 26 10
 R3  41 26       56K
 Q1  14 41 12    QNN
 R4  14  6       22K
 Q11  6 18  9    QNP
 Q10  2  6 42    QNP
 M1   2 25 13 13 MOSFET
 R5  13 21       1K
 V2  21 12       DC 0
 F1  12  3       V2 1
 Q8   9 16 20    QNN
 Q9  18  2 20    QNN
 R6   9 16       100K
 R7  16 12       100K
 R8  20 12       100K
 F2  12  3       V4 1
 V4   9 42  0
 Q12  6  6  9    QNP
 E3  22 12  4 10 1E5
 R10 22 24       5K
 Q13  8 24  5    QNN
 R13 10  4       1E7
 RIN  1 11       10MEG
.ENDS LM29x7
